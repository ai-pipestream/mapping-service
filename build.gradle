import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.plugins.signing.Sign

plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'jacoco'
    alias(libs.plugins.nmcp.single)
    alias(libs.plugins.axion.release)
    alias(libs.plugins.proto.toolchain)
}

scmVersion {
    tag {
        prefix = 'v'
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

def pipestreamBomVersion = findProperty('pipestreamBomVersion') ?: '0.7.10'

// Proto toolchain configuration - fetches from Git and generates Java/gRPC code
pipestreamProtos {
    sourceMode = 'git'

    modules {
        register("common") {
            gitRepo = "https://github.com/ai-pipestream/pipestream-protos.git"
            gitRef = "main"
            gitSubdir = "common"
        }
        register("admin") {
            gitRepo = "https://github.com/ai-pipestream/pipestream-protos.git"
            gitRef = "main"
            gitSubdir = "admin"
        }
        // Needed for module RPC return types like ProcessDataResponse (used by ProcessingBufferInterceptor)
        register("pipeline-module") {
            gitRepo = "https://github.com/ai-pipestream/pipestream-protos.git"
            gitRef = "main"
            gitSubdir = "pipeline-module"
        }
    }
}

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    // Import pipestream BOM to align all managed versions
    // Version defined in gradle.properties as single source of truth
    implementation platform("ai.pipestream:pipestream-bom:${pipestreamBomVersion}")

    implementation libs.quarkus.grpc
    implementation libs.quarkus.arc
    implementation libs.memoryfilesystem
    // Needed by lifted MappingHelperJsonSupport (and generally useful for Quarkus)
    implementation 'io.quarkus:quarkus-jackson'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-stork'
    implementation 'io.quarkus:quarkus-smallrye-openapi'

    // Service discovery / registration plumbing
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    // Pipestream platform libraries (managed via BOM/catalog)
    implementation 'ai.pipestream:pipestream-quarkus-devservices'
    implementation 'ai.pipestream:quarkus-dynamic-grpc'
    implementation 'ai.pipestream:pipestream-service-registration'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
}

group 'ai.pipestream'
version "${scmVersion.version}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    finalizedBy jacocoTestReport

    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco-quarkus.exec").get().asFile
    }
}

jacocoTestReport {
    // Don't count generated protobuf code towards service coverage.
    // We only want coverage for our runtime/service/util code, not the massive generated message classes.
    afterEvaluate {
        def excluded = [
                // any ".v1." package (directory /v1/) is assumed to be generated protobuf API
                '**/v1/**',
                // generated protos (packages)
                'ai/pipestream/data/**',
                'ai/pipestream/mapping/**',
                'ai/pipestream/parsed/**',
                'ai/pipestream/opensearch/**',
                // module service protos (ProcessDataResponse, etc.)
                'ai/pipestream/data/module/**',
        ]

        // Use main source set class dirs as the authoritative input, then filter.
        classDirectories.setFrom(files(sourceSets.main.output.classesDirs.files.collect { dir ->
            fileTree(dir: dir, exclude: excluded)
        }))
    }
    // JaCoCo doesn't reliably clean stale HTML output; wipe before regenerating so excludes take effect.
    doFirst {
        delete layout.buildDirectory.dir("reports/jacoco/test/html")
        delete layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
    }
    reports {
        xml.required = true
        html.required = true
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Configure Javadoc task
tasks.withType(Javadoc).configureEach { task ->
    def opts = task.options
    opts.encoding = 'UTF-8'
    opts.charSet = 'UTF-8'
    opts.locale = 'en'
    // Suppress missing Javadoc warnings for now
    opts.addStringOption('Xdoclint:-missing', '-quiet')
    // Exclude internal packages
    task.exclude('**/internal/**', '**/impl/**', '**/test/**')
}

// Enable resource filtering to replace @version@ in application.properties
// Use @ delimiters to avoid conflicts with ${} runtime property placeholders
processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}

processTestResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}

// Publishing configuration
publishing {
    publications {
        // Use a named publication to match signing configuration below
        create('maven', MavenPublication) {
            from components.java
            pom {
                name.set('Mapping Service')
                description.set('Document mapping and transformation service for the Pipeline platform')
                url.set('https://github.com/ai-pipestream/mapping-service')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/license/mit')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Engine Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/mapping-service.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/mapping-service.git')
                    url.set('https://github.com/ai-pipestream/mapping-service')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Conditionally add GitHub Packages repository when credentials are available
        def ghRepo = System.getenv('GITHUB_REPOSITORY') ?: 'ai-pipestream/mapping-service'
        def ghActor = System.getenv('GITHUB_ACTOR') ?: (project.findProperty('gpr.user') ?: System.getenv('GPR_USER'))
        def ghToken = System.getenv('GITHUB_TOKEN') ?: (project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN'))
        if (ghActor && ghToken) {
            maven {
                name = 'GitHubPackages'
                url = uri("https://maven.pkg.github.com/${ghRepo}")
                credentials {
                    username = ghActor
                    password = ghToken
                }
            }
        }
    }
}

signing {
    // Read org-level GPG secrets directly, so runnable apps don't need extra env mapping
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Ensure all signing tasks run before nmcp bundles and publishes artifacts
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
